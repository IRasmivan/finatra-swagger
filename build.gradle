ext.scalaVersionMain = scalaVersion.substring(0, scalaVersion.lastIndexOf("."))

apply plugin: 'scala'

repositories {
  mavenLocal()
  mavenCentral()

  maven { url "http://maven.twttr.com" }
}

dependencies {
  compile "org.scala-lang:scala-library:${scalaVersion}",
    'com.wordnik:swagger-core:1.5.3-M1'

  testCompile "org.scalatest:scalatest_${scalaVersionMain}:2.2.4"

  if(finatraVersion.startsWith("1")) {
    compile "com.twitter:finatra_${scalaVersionMain}:${finatraVersion}"
  } else {
    compile "com.twitter.finatra:finatra-http_${scalaVersionMain}:${finatraVersion}"
  }
}

sourceSets {
  main {
    scala {
      srcDirs = ['src/main/scala', "src/main/scala_${scalaVersionMain}"]
    }
  }
  test {
    scala {
      srcDirs = ['src/test/scala', "src/test/scala_${scalaVersionMain}"]
    }
  }
}

compileJava {
  targetCompatibility = "1.7"
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.useAnt = false
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}

apply from: 'publish.gradle'