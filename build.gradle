apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.xiaodongw'

version = '0.1.0'

repositories {
  mavenLocal()
  mavenCentral()

  maven { url "http://maven.twttr.com" }
}

dependencies {
  compile 'org.scala-lang:scala-library:2.10.4',
    'com.twitter:finatra_2.10:1.6.0',
    'com.wordnik:swagger-core:1.5.3-M1',
    files('libs/swagger-scala-module-1.0.0-SNAPSHOT.jar')

  testCompile 'org.scalatest:scalatest_2.10:2.2.4'
}

compileJava {
  targetCompatibility = "1.7"
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.useAnt = false
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Finatra Swagger'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Add Swagger support for Finatra to generate REST API docuemnt'
        url 'https://github.com/xiaodongw/finatra-swagger/'

        scm {
          connection 'https://github.com/xiaodongw/finatra-swagger.git'
          developerConnection 'https://github.com/xiaodongw/finatra-swagger.git'
          url 'https://github.com/xiaodongw/finatra-swagger.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'xiaodongw'
            name 'Xiaodong Wang'
            email 'xiaodongw79@gmail.com'
          }
        }
      }
    }
  }
}