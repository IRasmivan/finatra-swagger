if(project.hasProperty("scalaVersion")) {
  ext.scalaVersionMain = scalaVersion.substring(0, scalaVersion.lastIndexOf("."))
} else {
  ext.scalaVersionMain = "2.10"
  ext.scalaVersion = "2.10.4"
}

if(!project.hasProperty("finatraVersion")) {
  ext.finatraVersion = "1.6.0"
}

ext.finatraVersionMain = finatraVersion.substring(0, 1)

println("Scala Version: ${scalaVersion}")
println("Scala Version Main: ${scalaVersionMain}")
println("Finatra Version: ${finatraVersion}")
println("Finatra Version Main: ${finatraVersionMain}")

apply plugin: 'scala'

repositories {
  mavenLocal()
  mavenCentral()

  maven { url "http://maven.twttr.com" }
}

dependencies {
  compile "org.scala-lang:scala-library:${scalaVersion}",
    'com.wordnik:swagger-core:1.5.3-M1'

  testCompile "org.scalatest:scalatest_${scalaVersionMain}:2.2.4",
    'joda-time:joda-time:2.7'

  if(finatraVersion.startsWith("1")) {
    compile "com.twitter:finatra_${scalaVersionMain}:${finatraVersion}"
  } else {
    compile "com.twitter.finatra:finatra-http_${scalaVersionMain}:${finatraVersion}"
  }
}

sourceSets {
  main {
    scala {
      srcDirs = ['src/main/scala', "src/main/finatra_${finatraVersionMain}"]
    }
  }
  test {
    scala {
      srcDirs = ['src/test/scala', "src/test/finatra_${finatraVersionMain}"]
    }
  }
}

compileJava {
  targetCompatibility = "1.7"
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.useAnt = false
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}

apply from: 'publish.gradle'